// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © gorx1

//@version=5
indicator('Quick scan for drift', 'QSFD', timeframe = '')

source = input       (close                                                          )
length = input       (256                                                            )
data   = input.string('Original', options = ['Original', 'Differenced', 'Integrated'])

src = if data == 'Differenced'
    source - source[1]
else  if data == 'Integrated'
    source + source[1]
else
    source

len = length < 1 ? bar_index + 1 : length

qsfd(src, len) =>
    slope   = (src - nz(src[len])) / len
    dev_max = 0.0
    dev_min = 0.0

    for i = 0 to len - 1
        dev      = src[i] - (src - slope * i)
        dev_max := math.max(dev, dev_max)
        dev_min := math.min(dev, dev_min)

    slope / ((dev_max - dev_min) / len)

drift = nz(qsfd(src, len), 1)

plot(drift, 'Drift', color.purple, 2, plot.style_cross)

hline(-7.5, display = display.none)
hline(-1.5                        )
hline(-0.5                        )
hline( 0.                         )
hline( 0.5                        )
hline( 1.5                        )
hline( 7.5, display = display.none)
