// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © ILuvMarkets


//@version=5
//Author @ILuvMarkets
indicator("Seasonality DOW", "Seasonality DOW", format = format.volume)

if not timeframe.isdaily
    runtime.error("Please switch timeframe to Daily")

i_year_start = input(2000, "Start Year")
i_text_size = input.string(size.auto, "Text Size", [size.auto, size.tiny, size.small, size.normal, size.large, size.huge])

var year_start_ = math.max(year, i_year_start)
var no_years_ = year(timenow) - year_start_ + 1
var matrix<float> data_  =  matrix.new<float>(no_years_,7,0.0)
var table table_ = na
var text_color_ = color.white
var currentDayColor = color.orange
var bg_color_ = color.gray

//Debug
// plotchar(dayofweek,"DOW","", location.top)  // Sun-1 .... Sat- 7 

chg_pct_ = request.security(syminfo.tickerid, "D", nz(close/close[1]-1))
if year >= year_start_
    cur_val_ = matrix.get(data_, year-year_start_, dayofweek-1)
    matrix.set(data_, year-year_start_, dayofweek-1, cur_val_ + chg_pct_)

if barstate.islast
    table_ := table.new(position.middle_center, 13, year - year_start_ + 3, border_width = 1)
    table.cell(table_, 1, 0, "Sun", text_color = dayofweek(timenow) == 1 ? currentDayColor :  text_color_, bgcolor = bg_color_, text_size = i_text_size)
    table.cell(table_, 2, 0, "Mon", text_color = dayofweek(timenow) == 2 ? currentDayColor :  text_color_, bgcolor = bg_color_, text_size = i_text_size)
    table.cell(table_, 3, 0, "Tue", text_color = dayofweek(timenow) == 3 ? currentDayColor :  text_color_, bgcolor = bg_color_, text_size = i_text_size)
    table.cell(table_, 4, 0, "Wed", text_color = dayofweek(timenow) == 4 ? currentDayColor :  text_color_, bgcolor = bg_color_, text_size = i_text_size)
    table.cell(table_, 5, 0, "Thu", text_color = dayofweek(timenow) == 5 ? currentDayColor :  text_color_, bgcolor = bg_color_, text_size = i_text_size)
    table.cell(table_, 6, 0, "Fri", text_color = dayofweek(timenow) == 6 ? currentDayColor :  text_color_, bgcolor = bg_color_, text_size = i_text_size)
    table.cell(table_, 7, 0, "Sat", text_color = dayofweek(timenow) == 7 ? currentDayColor :  text_color_, bgcolor = bg_color_, text_size = i_text_size)

    for row_ = 0 to no_years_ - 1
        table.cell(table_, 0, 1+row_, str.tostring(row_ + year_start_), text_color = text_color_, bgcolor = bg_color_, text_size = i_text_size)
        for col_ = 0 to 6
            val_ = matrix.get(data_, row_, col_) 
            val_bgcolor_ = val_ > 0 ? color.green : val_ < 0 ? color.red : color.gray 
            table.cell(table_, 1+col_, 1+row_, str.format("{0,number,###.##%}", val_), bgcolor = color.new(val_bgcolor_,80), text_color = val_bgcolor_ , text_size = i_text_size)
            true

    //Aggregates
    row_ = no_years_ + 1
    table.cell(table_, 0, row_, "SUM", text_color = text_color_, bgcolor = bg_color_, text_size = i_text_size)
    for col_ = 0 to 6
        arr_ = matrix.col(data_, col_) 
        val_ = array.sum(arr_)
        val_color_ = val_ > 0 ? color.green : val_ < 0 ? color.red : color.gray 
        table.cell(table_, 1+col_, row_, str.format("{0,number,###.##%}", val_), bgcolor = color.new(val_color_,50), text_color = val_color_, text_size = i_text_size)
        true